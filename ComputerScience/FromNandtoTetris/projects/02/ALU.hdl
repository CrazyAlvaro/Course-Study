// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs,
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    // get 16-bit zero
    Not16(in=x, out=notx);
    And16(a=x, b=notx, out=zero);

    // get zx, zy output
    Mux16(a=x, b=zero, sel=zx, out=zxout);
    Mux16(a=y, b=zero, sel=zy, out=zyout);

    // get nx, ny output
    Not16(in=zxout, out=ngzxout);
    Not16(in=zyout, out=ngzyout);
    Mux16(a=zxout, b=ngzxout, sel=nx, out=nxout);
    Mux16(a=zyout, b=ngzyout, sel=ny, out=nyout);

    // get f output
    Add16(a=nxout, b=nyout, out=xysum);
    And16(a=nxout, b=nyout, out=xyand);
    Mux16(a=xyand, b=xysum, sel=f, out=fout);

    // get no output
    Not16(in=fout, out=nfout);
    Mux16(a=fout, b=nfout, sel=no, out=fkout);
    Or16(a=fkout, b=zero, out=out);

    // zr
    Or16(a=fkout, b=zero, out[0..7]=bits1);
    Or16(a=fkout, b=zero, out[8..15]=bits2);
    Or8Way(in=bits1, out=or1);
    Or8Way(in=bits2, out=or2);
    Or(a=or1, b=or2, out=nzout);
    Not(in=nzout, out=zr);

    // ng
    Not16(in=fkout, out[15]=nng);
    Not(in=nng, out=ng);
}